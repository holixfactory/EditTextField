apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'EditTextField = EditText + AndroidView. ' +
                            '삼성키보드 문구추천 + Compose TextField 사용 시 발생하는 문제를 해결합니다.'
                    url = 'https://github.com/holixfactory/EditTextField'
                    licenses {
                        license {
                            name = 'Apache 2.0 License'
                            url = 'https://github.com/holixfactory/EditTextField/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'workspace'
                            name = 'Kimin Ryu'
                            email = 'ryu@holix.com'
                        }
                        developer {
                            id = 'Heyday7'
                            name = 'Taehyup Ko'
                            email = 'thko@holix.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/holixfactory/EditTextField.git'
                        developerConnection =
                                'scm:git:ssh://github.com/holixfactory/EditTextField.git'
                        url = 'https://github.com/holixfactory/EditTextField/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
